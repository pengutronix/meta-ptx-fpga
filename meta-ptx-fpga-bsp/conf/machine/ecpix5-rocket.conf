#@TYPE: Machine
#@NAME: ecpix5-rocket
#@DESCRIPTION: LambdaConcept ECPIX-5 ECP5 Development Board

DEFAULTTUNE = "riscv64"
FPGA_FAMILY ?= "ecp5"

require conf/machine/include/riscv/tune-riscv.inc

TUNE_CCARGS .= "-march=rv64ima -mabi=lp64"

SERIAL_CONSOLES ?= "115200;ttyLXU0"

PREFERRED_PROVIDER_virtual/kernel = "linux-ecpix5"
# riscv-pk uses the vmlinux elf file as payload
KERNEL_IMAGETYPE = "vmlinux"

# The initramfs needs to be bundled in kernel, because this has to be part of
# the riscv-pk payload
INITRAMFS_IMAGE = "core-image-minimal"
INITRAMFS_IMAGE_BUNDLE = "1"

IMAGE_FSTYPES += "cpio"

# depend on the bitstream/firmware
EXTRA_IMAGEDEPENDS += "litex-boards-rocket"

# this is needed to get meta-riscv's glibc append
MACHINEOVERRIDES .= ":qemuriscv64"

# FIXME The sysvinit init scripts fail to boot to an interactive shell. Use
# Busybox for now until this has been fixed.
VIRTUAL-RUNTIME_dev_manager = "busybox-mdev"
VIRTUAL-RUNTIME_login_manager = "busybox"
VIRTUAL-RUNTIME_init_manager = "busybox"
VIRTUAL-RUNTIME_initscripts = "initscripts"
VIRTUAL-RUNTIME_keymaps = "keymaps"
DISTRO_FEATURES_BACKFILL_CONSIDERED += "sysvinit"

# FIXME Latest litex does not build with rocket
SRCREV_pn-litex-native = "080ecad522ad2dec43295517f5111822022554f4"
