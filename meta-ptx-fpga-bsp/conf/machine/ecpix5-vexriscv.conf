#@TYPE: Machine
#@NAME: ecpix5-vexriscv
#@DESCRIPTION: LambdaConcept ECPIX-5 ECP5 Development Board

DEFAULTTUNE ?= "riscv32"
FPGA_FAMILY ?= "ecp5"

require conf/machine/include/riscv/tune-riscv.inc

TUNE_CCARGS .= "-march=rv32ima -mabi=ilp32 -D__vexriscv__"

SERIAL_CONSOLES ?= "115200;ttyLXU0"

RISCV_SBI_PLAT ?= "litex/vexriscv"

PREFERRED_PROVIDER_virtual/kernel = "linux-ecpix5"
KERNEL_IMAGETYPE = "Image"
EXTERNAL_KERNEL_DEVICETREE = "litex-vexriscv-ecpix5.dtb"
PREFERRED_PROVIDER_virtual/dtb ?= "devicetree"

IMAGE_FSTYPES += "cpio wic wic.gz"

# The wks depends on ptxsoc-vexriscv, since the boot.json is installed by the
# recipe as a hack.
WKS_FILE_DEPENDS ?= " \
    opensbi \
    e2fsprogs-native \
    devicetree \
    ptxsoc-vexriscv \
    virtual/kernel \
"

IMAGE_BOOT_FILES ?= " \
    Image \
    fw_jump.bin \
    devicetree/litex-vexriscv-ecpix5.dtb \
    boot.json \
"

WKS_FILE ?= "ecpix5-sdcard.wks"

# The bitstream is not part of the SD card image but must be flashed into the
# SPI flash. The explicit dependency ensures that the bitstream is build.
EXTRA_IMAGEDEPENDS += "ptxsoc-vexriscv"

# this is needed to get meta-riscv's glibc append
MACHINEOVERRIDES .= ":qemuriscv32"
